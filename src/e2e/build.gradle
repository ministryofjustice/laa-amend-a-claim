plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    main {
        java.srcDir 'main/java'
        resources.srcDir 'main/resources'
    }
    test {
        java.srcDir 'test/java'
        resources.srcDir 'test/resources'
        output.resourcesDir = "build/resources/test"
    }
}

dependencies {
    implementation 'com.microsoft.playwright:playwright:1.58.0'
    implementation 'com.deque.html.axe-core:playwright:4.11.1'
    testImplementation 'org.webjars.npm:axe-html-reporter:2.2.11'
    implementation 'io.github.cdimascio:dotenv-java:3.2.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'

    testImplementation 'io.rest-assured:rest-assured:6.0.0'

    // Allure + JUnit 5
    testImplementation 'io.qameta.allure:allure-junit5:2.32.0'
    testImplementation 'org.postgresql:postgresql:42.7.9'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    // Ensure test files + Allure always re-run
    outputs.upToDateWhen { false }

    // Allure auto-activation
    systemProperty "junit.jupiter.extensions.autodetection.enabled", "true"
    systemProperty "junit.platform.listeners.autodetection.enabled", "true"
    systemProperty "allure.results.directory", "$buildDir/allure-results"

    // Playwright settings
    systemProperty "browser", System.getenv("BROWSER") ?: "chromium"
    systemProperty "headless", System.getenv("HEADLESS") ?: "true"
    systemProperty "baseUrl", System.getenv("UI_BASE_URL") ?: ""

    // Enable readable test output in CI
    testLogging {
        events "passed", "failed", "skipped"
        exceptionFormat "full"
        showStandardStreams = true
    }

    doFirst {
        delete fileTree("$buildDir/allure-results")
        delete fileTree("$buildDir/axe-reports")
    }

    doLast {
        def jsonDir = file("$buildDir/axe-reports/json")
        def htmlDir = file("$buildDir/axe-reports/html")

        if (!jsonDir.exists()) {
            println "No axe JSON reports found"
            return
        }

        htmlDir.mkdirs()

        jsonDir.eachFileMatch(~/.*\.json/) { File jsonFile ->
            exec {
                commandLine "node",
                        "generateAxeReport.js",
                        jsonFile.absolutePath,
                        jsonFile.name.replace(".json", ".html")
            }
        }
    }
}

tasks.register('allureReport', Exec) {
    dependsOn test
    commandLine 'allure',
        'generate',
        "$buildDir/allure-results",
        '-o', "$buildDir/allure-report",
        '--clean'
}

tasks.register('allureServe', Exec) {
    dependsOn test
    commandLine 'allure', 'serve', "$buildDir/allure-results"
}