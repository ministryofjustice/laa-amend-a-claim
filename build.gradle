plugins {
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin' version '2.0.5'
    id "com.diffplug.spotless" version "8.2.1"
    id "com.palantir.java-format-spotless" version "2.85.0"
    id "io.sentry.jvm.gradle" version "6.0.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

checkstyle {
    toolVersion = '13.0.0'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.3.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'

    // Data claims API
    implementation 'uk.gov.justice.laa.datastewardship.payments:data-claims-api-models:d694928'
    // Removes 'unknown enum constant RequiredMode.REQUIRED' warning (springdoc is a transitive dependency of data-claims-api-models)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.webjars.npm:govuk-frontend:5.14.0'
    implementation 'org.webjars.npm:ministryofjustice__frontend:7.1.0'

    implementation 'io.netty:netty-codec-http:4.2.9.Final'
    implementation 'io.netty:netty-codec-http2:4.2.9.Final'

    implementation 'ch.qos.logback:logback-core:1.5.27'
    implementation 'ch.qos.logback:logback-classic:1.5.27'

    implementation("io.vavr:vavr:0.11.0")
    implementation("uk.gov.justice.laadata.providers:providers-api:1.23.0")

    // Sentry observability for Spring Boot 4
    implementation enforcedPlatform('io.sentry:sentry-bom:8.26.0')
    implementation 'io.sentry:sentry-spring-boot-4'

    compileOnly 'org.projectlombok:lombok'


    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // test dependencies
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:5.0.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.assertj:assertj-core:3.27.7'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.jmockit:jmockit:1.50'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.jsoup:jsoup:1.22.1'
    testImplementation 'com.github.codemonstur:embedded-redis:1.4.3'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// Skip e2e tasks during regular build/test
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':build') || graph.hasTask(':test')) {
        graph.allTasks.each { task ->
            if (task.path.contains(':e2e:')) {
                task.enabled = false
            }
        }
    }
}

tasks.register('checkstyleAll') {
    group = 'verification'
    description = 'Runs checkstyleMain, checkstyleTest, and checkstyleIntegrationTest.'
    dependsOn tasks.matching { it.name in ['checkstyleMain', 'checkstyleTest', 'checkstyleIntegrationTest'] }
}

tasks.named('build') {
    dependsOn tasks.named('checkstyleAll')
}
